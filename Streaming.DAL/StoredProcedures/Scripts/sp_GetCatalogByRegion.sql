IF EXISTS (SELECT * FROM sysobjects WHERE type = 'P' AND name = 'sp_GetCatalogByRegion')
	BEGIN
		DROP PROCEDURE sp_GetCatalogByRegion
	END
GO

CREATE PROCEDURE sp_GetCatalogByRegion
	@PAGE_NUMBER INT = 1,
	@PAGE_SIZE INT = 50,
	@ID_LANGUAGE INT

AS

BEGIN

SET NOCOUNT ON;
DECLARE @OFFSET INT = (@PAGE_NUMBER - 1) * @PAGE_SIZE;

WITH CATALOG AS (
	SELECT 
	CR.NAME, 
	CR.SYNOPSIS, 
	CR.ID_FILM,
	CR.ID_SERIES,
	CASE WHEN CR.ID_FILM IS NOT NULL 
		THEN F.THUMBNAIL 
		ELSE S.THUMBNAIL 
	END THUMBNAIL,
	CASE WHEN CR.ID_FILM IS NOT NULL 
			THEN STRING_AGG(CONVERT(VARCHAR, CAT_FILM.ID_CATEGORY) + '-' + CAT_FILM.NAME, '|') 
			ELSE STRING_AGG(CONVERT(VARCHAR, CAT_SERIES.ID_CATEGORY) + '-' + CAT_SERIES.NAME, '|') 
	END CATEGORIES,
	COUNT(*) OVER() AS TOTAL

	FROM CATALOG_REGION CR

	LEFT JOIN FILM F
		ON CR.ID_FILM = F.ID_FILM
	LEFT JOIN CATALOG_CATEGORY CCA_FILM
		ON F.ID_FILM = CCA_FILM.ID_FILM
	LEFT JOIN CATEGORY CAT_FILM
		ON CCA_FILM.ID_CATEGORY = CAT_FILM.ID_CATEGORY

	LEFT JOIN SERIES S
		ON CR.ID_SERIES = S.ID_SERIES
	LEFT JOIN CATALOG_CATEGORY CCA_SERIES
		ON S.ID_SERIES = CCA_SERIES.ID_SERIES
	LEFT JOIN CATEGORY CAT_SERIES
		ON CCA_SERIES.ID_CATEGORY = CAT_SERIES.ID_CATEGORY

	WHERE
		CR.ID_LANGUAGE = @ID_LANGUAGE

	GROUP BY 
		CR.NAME,
		CR.SYNOPSIS, 
		CR.ID_FILM,
		F.THUMBNAIL,
		CR.ID_SERIES,
		S.THUMBNAIL
)

SELECT 
	NAME,
	SYNOPSIS,
	ID_FILM,
	ID_SERIES,
	THUMBNAIL,
	CATEGORIES,
	TOTAL
FROM 
	CATALOG
ORDER BY
	NAME
OFFSET @OFFSET ROWS

FETCH NEXT @PAGE_SIZE ROWS ONLY;

END
