IF EXISTS (SELECT * FROM sysobjects WHERE type = 'P' AND name = 'sp_GetCatalogByRegion')
	BEGIN
		DROP PROCEDURE sp_GetCatalogByRegion
	END
GO

CREATE PROCEDURE sp_GetCatalogByRegion
	@PAGE_NUMBER INT = 1,
	@PAGE_SIZE INT = 50,
	@ID_LANGUAGE INT,
	@ID_CATEGORY INT,
	@KIDS_CONTENT BIT,
	@SEARCH VARCHAR(100)

AS

BEGIN

SET NOCOUNT ON;

DECLARE @OFFSET INT = (@PAGE_NUMBER - 1) * @PAGE_SIZE;
DECLARE @SEARCH_LIKE VARCHAR(102) = '%'+@SEARCH+'%';

WITH CATALOG AS (
SELECT
	CR.NAME, 
	CR.SYNOPSIS, 
	CR.ID_FILM,
	CR.ID_SERIES,
	CASE WHEN CR.ID_FILM IS NOT NULL 
		THEN F.THUMBNAIL 
		ELSE S.THUMBNAIL 
	END THUMBNAIL,
	CASE WHEN FILM_CATEGORIES.CATEGORIES IS NOT NULL 
			THEN FILM_CATEGORIES.CATEGORIES
			ELSE SERIES_CATEGORIES.CATEGORIES
	END CATEGORIES,
	COUNT(*) OVER() AS TOTAL

FROM 
	CATALOG_REGION CR
	LEFT JOIN FILM F
		ON CR.ID_FILM = F.ID_FILM
	LEFT JOIN (
		SELECT 
			F_CATEGORIES.ID_FILM,
			F_CATEGORY.ID_CATEGORY,
			STRING_AGG(CONVERT(VARCHAR, F_CATEGORY.ID_CATEGORY) + '-' + F_CATEGORY.NAME, '|') AS CATEGORIES
		FROM 
			CATALOG_CATEGORY F_CATEGORIES
		INNER JOIN CATEGORY F_CATEGORY 
			ON F_CATEGORIES.ID_CATEGORY = F_CATEGORY.ID_CATEGORY
		GROUP BY 
			F_CATEGORIES.ID_FILM, F_CATEGORY.ID_CATEGORY
	) FILM_CATEGORIES ON FILM_CATEGORIES.ID_FILM = F.ID_FILM
	LEFT JOIN (
		SELECT
			CAST_FILM.ID_FILM,
			STRING_AGG(CONVERT(VARCHAR, CAST_FILM.NAME), '|') AS CASTING
		FROM 
			CAST CAST_FILM
		GROUP BY 
			CAST_FILM.ID_FILM
	) CAST_FILM ON CAST_FILM.ID_FILM = F.ID_FILM

	LEFT JOIN SERIES S
		ON CR.ID_SERIES = S.ID_SERIES
	LEFT JOIN (
		SELECT 
			S_CATEGORIES.ID_SERIES,
			S_CATEGORY.ID_CATEGORY,
			STRING_AGG(CONVERT(VARCHAR, S_CATEGORY.ID_CATEGORY) + '-' + S_CATEGORY.NAME, '|') AS CATEGORIES
		FROM 
			CATALOG_CATEGORY S_CATEGORIES
		INNER JOIN CATEGORY S_CATEGORY 
			ON S_CATEGORIES.ID_CATEGORY = S_CATEGORY.ID_CATEGORY
		GROUP BY 
			S_CATEGORIES.ID_SERIES, S_CATEGORY.ID_CATEGORY
	) SERIES_CATEGORIES ON SERIES_CATEGORIES.ID_SERIES = S.ID_SERIES
	LEFT JOIN (
		SELECT
			CAST_SERIES.ID_SERIES,
			STRING_AGG(CONVERT(VARCHAR, CAST_SERIES.NAME), '|') AS CASTING
		FROM 
			CAST CAST_SERIES
		GROUP BY 
			CAST_SERIES.ID_SERIES
	) CAST_SERIES ON CAST_SERIES.ID_SERIES = S.ID_SERIES

WHERE
	CR.ID_LANGUAGE = @ID_LANGUAGE
	AND (@ID_CATEGORY IS NULL OR @ID_CATEGORY = 0 OR 
		FILM_CATEGORIES.ID_CATEGORY = @ID_CATEGORY OR SERIES_CATEGORIES.ID_CATEGORY = @ID_CATEGORY)
	AND (F.KIDS_CONTENT = @KIDS_CONTENT OR S.KIDS_CONTENT = @KIDS_CONTENT)
	AND (@SEARCH IS NULL OR @SEARCH = '' OR
		CR.NAME LIKE @SEARCH_LIKE OR F.NAME LIKE @SEARCH_LIKE OR S.NAME LIKE @SEARCH_LIKE OR
		CAST_FILM.CASTING LIKE @SEARCH_LIKE OR CAST_SERIES.CASTING LIKE @SEARCH_LIKE)
)

SELECT 
	NAME,
	SYNOPSIS,
	ID_FILM,
	ID_SERIES,
	THUMBNAIL,
	CATEGORIES,
	TOTAL
FROM 
	CATALOG
ORDER BY
	NAME
OFFSET @OFFSET ROWS

FETCH NEXT @PAGE_SIZE ROWS ONLY;

END
